import fabric/fabric_types;

export {
	getFabricValue(env : FabricEnv, name : string) -> Maybe<flow>;
	captureFabricValue(env : FabricEnv, name : string, value : flow) -> DynamicBehaviour<flow>;

	nextFabricValue(env : FabricEnv, name : string, value : flow) -> void;

	// Helpers
	getFabricBoolValue(env : FabricEnv, name : string) -> bool;
	getFabricIntValue(env : FabricEnv, name : string) -> int;
	getFabricStringValue(env : FabricEnv, name : string) -> string;
}

getFabricDynamicValue(env : FabricEnv, name : string) -> Maybe<DynamicBehaviour<flow>> {
	mvalue = lookupTree(^(env.values), name);
	mvalue ?? {
		Some(mvalue);
	} : {
		switch (env.parent) {
			None(): None();
			Some(p): getFabricDynamicValue(p, name);
		}
	}
}

getFabricValue(env : FabricEnv, name : string) -> Maybe<flow> {
	mvalue = getFabricDynamicValue(env, name);
	mvalue ?? {
		Some(getValue(mvalue));
	} : mvalue
}

getFabricValueRequired(env : FabricEnv, name : string, def : flow) -> flow {
	mvalue = getFabricValue(env, name);
	mvalue ?? {
		mvalue
	} : {
		println("Error: Unknown " + name);
		def;
	}
}

getFabricBoolValue(env : FabricEnv, name : string) -> bool {
	value = getFabricValueRequired(env, name, false);
	if (value == true || value == false) value
	else {
		println("Error: " + name + " is not a bool: " + toString(value));
		false
	}
}

getFabricIntValue(env : FabricEnv, name : string) -> int {
	getFabricValueRequired(env, name, 0);
}

getFabricStringValue(env : FabricEnv, name : string) -> string {
	getFabricValueRequired(env, name, "");
}


captureFabricValue(env : FabricEnv, name : string, value : flow) -> DynamicBehaviour<flow> {
	mvalue = getFabricDynamicValue(env, name);
	mvalue ?? {
		nextDistinct(mvalue, value);
		mvalue
	} : {
		dyn = make(value);
		env.values := setTree(^(env.values), name, dyn);
		dyn;
	}
}

nextFabricValue(env : FabricEnv, name : string, value : flow) -> void {
	mvalue = getFabricDynamicValue(env, name);
	switch (mvalue) {
		None(): {
			println("Error: Unknown value to next: " + name);
		}
		Some(v): {
			nextDistinct(v, value);
		}
	}
}
