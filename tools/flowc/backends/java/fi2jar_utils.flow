import tools/flowc/backends/common;

export {
	fi2javaGradleDeps2JarDeps(cfg : FiJavaConfig) -> [string];

	runGradle(args : [string], currentWorkingDirectory : string, onStdOutLine : (out : string) -> void, onStdErr : (error : string) -> void, onExit : (errocode : int) -> void) -> native;

	// Returns the gradle version like '6.8' in case gradle is installed and -1.0 otherwise.
	fcGradleVersion() -> double;
}

fi2javaGradleDeps2JarDeps(cfg : FiJavaConfig) -> [string] {
	filtermap(cfg.dependencies, \dep ->
		if (!startsWith(dep, "gradle")) None() else {
			gradle_dep = substring(dep, 7, strlen(dep) - 8);
			components = strSplit(gradle_dep, ":");
			if (length(components) != 3) None() else {
				Some(components[1] + "-" + components[2] + ".jar");
			}
		}
	);
}

execGradle(args : [string], currentWorkingDirectory : string, onStdOutLine : (out : string) -> void, onStdErr : (error : string) -> void) -> int {
	println("Run Gradle: " + toString(windows()));
	if (windows()) {
		cmd_args = concat(["/c", "gradle"], args);
		println(">> cmd " + strGlue(cmd_args, " "));
		execSystemProcess("cmd", cmd_args, currentWorkingDirectory, onStdOutLine, onStdErr);
	} else {
		execSystemProcess("gradle", args, currentWorkingDirectory, onStdOutLine, onStdErr);
	}
}

runGradle(args : [string], currentWorkingDirectory : string, onStdOutLine : (out : string) -> void, onStdErr : (error : string) -> void, onExit : (errocode : int) -> void) -> native {
	println("Run Gradle: " + toString(windows()));
	if (windows()) {
		cmd_args = concat(["/c", "gradle"], args);
		println(">> cmd " + strGlue(cmd_args, " "));
		runSystemProcess("cmd", cmd_args, currentWorkingDirectory, onStdOutLine, onStdErr, onExit);
	} else {
		runSystemProcess("gradle", args, currentWorkingDirectory, onStdOutLine, onStdErr, onExit);
	}
}


fc_gradle_version : ref Maybe<double> = ref None();

fcGradleVersion() -> double {
	lookup_version = \-> {
		out = ref "";
		err = ref false;
		exitCode = execGradle(["-version"], ".", \o -> out := ^out + "\n" + o, \e -> err := true);
		version = if (exitCode == 0 && !^err) ^out else "";

		// Find the first line with 'gradle' keyword, following by a double
		gradle_version = filtermap(
			strSplit(version, "\n"),
			\line0 -> {
				line = toLowerCase(line0);
				ind = strIndexOf(line, "gradle");
				if (ind == -1) None() else {
					// A substring after
					ver_str0 = substring(line, ind + 7, strlen(line) - (ind + 7));
					// Remove a trailing dot, if exists and extra spaces
					ver_str = trim2(ver_str0, ". ");
					// Check if ver_str contains only digits and dot
					if (isDouble(ver_str)) Some(ver_str) else None();
				}
			}
		);
		if (length(gradle_version) == 0) {
			-1.0;
		} else {
			s2d(gradle_version[0]);
		}
	}
	onlyOnce(fc_gradle_version, lookup_version);
}
