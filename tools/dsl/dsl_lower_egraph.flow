import tools/dsl/registry/dsl_registry;
import tools/dsl/registry/dsl_specification;
import tools/dsl/dsl_match_egraph;
import tools/dsl/registry/dsl_run;
import tools/dsl/dsl_rules;

export {
	// Define a lowering from a given language to another, running the "to" program in the given language
	defineDslLowering(from : DslGrammar, to : DslGrammar, sep : string, rules : string) -> DslLowering;

	// Does a bottom-up visit of the e-graph, visiting the nodes that
	// match the language suffix, and then applying those nodes with the
	// result of their children
	lowerDslEGraph(e : DslAstEnv, eclass : int,
		specification : string,
		lowering : DslLowering,
	) -> Maybe<DslENode>;
}

defineDslLowering(from : DslGrammar, to : DslGrammar, sep : string, rules : string) -> DslLowering {
	trules = parseRules(from, to, rules, sep);
	patterns : List<DslReplacement> = mapList(getDslList(trules.rules), \rule -> {
		node = getDslNode(rule);
		pattern = node.args[0];
		replacement = node.args[1];
		DslReplacement(pattern, replacement);
	});

	DslLowering(patterns, "lambda");
}



lowerDslEGraph(env : DslAstEnv, eclass : int, 
	specification : string,
	lowering : DslLowering,
) -> Maybe<DslENode> {
	e = env.egraph;
	root = getDslEClassRoot(e, eclass);
	nodes = getDslEClassNodes(e, root);
	matching = set2array(filterSet(nodes, \n -> matchDslSpecification(n.language, specification)));
	if (length(matching) > 1) {
		println("We have more nodes than one");
	}
	rewritten = ref false;
	best = fold(matching, None(), \acc, preferred : DslENode -> {
		children = filtermap(preferred.args, \childClass -> {
			lowerDslEGraph(env, childClass, specification, lowering)
		});
		if (length(children) != length(preferred.args)) {
			println("ERROR: Expected more children in lowering");
			acc
		} else {
			term = combineENode(preferred, children);

			resultLanguage = resultDslTransformation(preferred.language, specification);
			rewritten := lowerDslENode(env, specification, resultLanguage, lowering, root, preferred, term) || ^rewritten;
			node = DslENode(
					resultLanguage,
					term,
					preferred.args
				);
			Some(node);
		}
	});
	if (!^rewritten && matching != []) {
		// println("Alright, we have to just directly lower for e" + i2s(eclass));
		// OK, we have to just lower it immediately
		node = matching[0];
		resultLanguage = resultDslTransformation(node.language, specification);
		newENode = DslENode(node with language = resultLanguage);
		newEclass = addDslENode(env.egraph, newENode);
		unionDslEClass(env.egraph, eclass, newEclass);
		{}
	}
	best;
}

// OK, we know this node is the right language. Process it
lowerDslENode(e : DslAstEnv, specification : string, resultLanguage : string, 
			lowering : DslLowering, eclass : int, node : DslENode, term : DslAst) -> bool {
	foldList(lowering.replacements, false, \acc, r : DslReplacement -> {
		acc || lowerDslENodeWithReplacement(e, specification, resultLanguage, lowering.toLanguage, r, eclass, term)
	});
}

lowerDslENodeWithReplacement(e : DslAstEnv, specification : string, resultLanguage : string, 
		toLanguage : string, replacement : DslReplacement, eclass : int, term : DslAst) -> bool {
	acc = DslENodeMatch(makeTree(), false);
	bindings = dslENodeMatch(acc, term, replacement.pattern);
	if (!bindings.fail) {
		astEnv = DslAstEnv(e with language = toLanguage, ast = replacement.replacement);
		
		// println("Lowering to " + toLanguage);
		desugared = performDslTransformations("desugar", "lambda", astEnv);
		// println(prettyDsl(desugared.ast));

		env = makeDslEnv();
		runtime = getDslLanguageRuntime(specification);
		nenv = DslEnv(env 
			with globals = mapTree(desugared.env, \a -> [a]), 
			locals = mergeTree(mergeTree(astEnv.env, bindings.bindings), runtime)
		);

		result = evaluateDsl(nenv, desugared.ast);

		if (false) {
			println("This match: " + prettyDsl(term) + " against " + prettyDsl(replacement.pattern));
			/*traverseInOrder(bindings.bindings, \name, val -> {
				println("  " + name + " = " + prettyDsl(val));
			});*/
			println("gives " + prettyDsl(result));
		}

		newEclass = addDslAst(e.egraph, resultLanguage, result);
		unionDslEClass(e.egraph, eclass, newEclass);
		true
	} else {
		false;
	}
}
