import fabric/fabric;

main() -> void {
	progressBar = \ -> {
		BSelect(["elapsedTime", "duration"], \env, args -> {
			elapsedTime = args[0];
			duration = args[1];
			progress = if (duration != 0.0 && duration >= elapsedTime) elapsedTime / duration else 1.0;
			BGroup([
				BMaterial(TRectangle([Fill(0xc0c0c0)], TFixed(256.0, 16.0))),
				BMaterial(TRectangle([Fill(0x0000c0)], TFixed(256.0 * progress, 16.0)))
			])
		})
	};

	ui = BLet("duration", 25.0,
		BLet("elapsedTime", 0.0,
			BLines([
				BTimer("elapsedTime", 0.1),
				BBCols([BText("Elapsed time: ", []), progressBar()]),
				BSelect(["elapsedTime", "duration"], \env, args -> BText(d2s(min(args[0], args[1])) + "s", [])),
				BBCols([BText("Duration", []), BSlider("duration", [MSliderRange(0.0, 50.0)])]),
				BTextButton("RESET", \env -> {
					nextFabricValue(env, "elapsedTime", 0.0);
				}, [], [])
			])
		)
	);
	brender(ui);
	{}
}